1 GOTO 8800 :REM SNAKE GAME IN GR
1290 D = PEEK(-16368)
1300 REM BOUCLE DE JEU 136 G 149 D
1310 KE = PEEK(49152)
1315 VTAB 22 : ? "SCORE :" , SC
1316 VTAB 23 : ? "APPLES TO EAT :"; NA : ? " - APPLES EATEN : "; AE
1317 J = J + 1 : REM TIMER BEFORE APPLE
1320 IF KE < 128 GOTO 1430
1330 IF KE = 136 AND DY = -1 THEN DY=0 : DX=-1 : GOTO 1410
1340 IF KE = 136 AND DY = 1 THEN DY=0 : DX = 1 : GOTO 1410
1350 IF KE = 136 AND DX = 1 THEN DX = 0 : DY = -1 : GOTO 1410
1360 IF KE = 136 AND DX = -1 THEN DX = 0 : DY = 1 : GOTO 1410
1370 IF KE = 149 AND DY = -1 THEN DY = 0 : DX = 1 : GOTO 1410
1380 IF KE = 149 AND DY = 1 THEN DY=0 : DX = -1 : GOTO 1410
1390 IF KE = 149 AND DX = 1 THEN DX = 0 : DY = 1 : GOTO 1410
1400 IF KE = 149 AND DX = -1 THEN DX = 0 : DY = -1 : GOTO 1410
1410 D = PEEK(-16368) : REM RESET KEYBOARD
1430 FOR C = SL TO 1 STEP -1 : SX(C+1) = SX(C): SY(C+1)=SY(C) : NEXT
1440 SX(1) = SX(1) + DX : SY(1) = SY(1) + DY
1445 IF J = LD THEN GOSUB 4010 : J = 0
1450 REM CHECK HEAD DRAW SNAKE
1460 CO = SCRN(SX(1),SY(1))
1465 COLOR = 13 : PLOT SX(1),SY(1)
1470 IF CO = 1 GOTO 9500 : REM WALL BUMP
1480 IF CO <> 9 GOTO 1610 
1490 REM APPLE EATEN
1500 SC = SC + 10
1510 REM SNAKE GROW
1540 SL = SL + 1 : REM 1 MORE PLACE 
1560 AE = AE + 1 : AC = AC - 1 : REM APPLE EATEN COUNTER
1570 IF NA = AE THEN LV = LV + 1: GOTO 9100
1580 GOTO 1300
1590 REM DRAW SNAKE
1610 COLOR = 0 : PLOT SX(SL+1),SY(SL+1)
1630 GOTO 1300
1999 END
2000 REM MAIN START
2010 REM CLEAR DATA
2020 FOR C = 1 TO 50: SX(C)=0: SY(C)=0 : NEXT
2030 LX = 20 : LY = 39 : DX = 0 : DY = 0 : SL = 3 : AE = 0
2040 SX(1)= 20 : SX(2)= 20 :SX(3)= 20 : REM INITIAL SNAKE X POS
2050 SY(1) = 39 : SY(2) = 39 : SY(3) = 39: REM INITIAL SNAKE Y POS
2060 REM MAIN LOOP
2070 GR
2080 REM DRAW BOUNDARIES
2090 COLOR = 1 : REM RED 
2100 HLIN 0,39 AT 0 : HLIN 0,39 AT 39 : VLIN 0,39 AT 0 : VLIN 0,39 AT 39
2110 VTAB 21 : A$ = "LEVEL " + STR$(LV) : GOSUB 7000
2120 FOR W = 1 to 1000 : NEXT:REM ON PATIENTE
2130 COLOR = 0 : HLIN 19,21 AT 39 : REM OUVERTURE DE LA PORTE
2140 DE = 3 : REM VARIABLE POUR L'ANIMATION
2150 FOR B = 1 TO DE
2160 COLOR = 13
2170 FOR A = 1 TO SL : PLOT SX(A),SY(A) : NEXT
2190 REM ON DECALE LES DONNEES
2200 FOR C = SL TO 1 STEP -1 : SX(C+1) = SX(C): SY(C+1)=SY(C) : NEXT
2210 SY(1) = SY(1)-1
2220 FOR W = 1 TO 25 : NEXT W 
2230 NEXT B
2240 COLOR = 1 : HLIN 0,39 AT 39 : REM FERMETURE DE LA PORTE
2250 COLOR = 0 : PLOT 20,38
2260 REM ANIMATION TANT QUE TETE N'EST PAS A 20
2270 DX = 0 : DY = -1
2280 FOR C = SL TO 1 STEP -1 : SX(C+1) = SX(C): SY(C+1)=SY(C) : NEXT
2290 SX(1) = SX(1) + DX : SY(1) = SY(1) + DY
2300 COLOR = 13 : PLOT SX(1),SY(1)
2310 COLOR = 0 : PLOT SX(SL+1),SY(SL+1)
2320 IF SY(1)<>20 THEN GOTO 2280
2330 REM FORCE SNAKE POS
2340 SX(1) = 20 : SY(1) = 20
2350 SX(2) = 20 : SY(2) = 21
2360 SX(3) = 20 : SY(3) = 22
2370 SX(4) = 20 : SY(4) = 23
2380 GOTO 1290
4000 REM ADD APPLE
4010 AX = INT(RND(I)*39) : AY = INT(RND(I)*39)
4020 IF SCRN(AX,AY)<> 0 GOTO 4010
4030 AC = AC + 1
4040 IF AC > 1  THEN NA = NA + 1
4060 COLOR = 9 : PLOT AX,AY
4070 RETURN
7000 REM PRINT CENTERED
7010 LS = LEN(A$)
7020 HTAB INT(20-(LS/2))
7030 PRINT A$
7040 RETURN
8800 REM VARIABLES DECLARATION
8810 SL = 3 : REM INITIAL SNAKE LENGTH
8820 DIM SX(50) : DIM SY(50) 
8830 LET AX = 0 : LET AY = 0 : LET AN = 1 : LET AE = 0 : REM APPLE PLACE AND NUMBER AND EATEN
8835 LET AC = 0 : REM APPLES CREATED
8840 LET W = 0 : REM VARIABLE POUR L'ATTENTE
8850 LET LV = 1 : REM LEVEL NUMBER
8860 LET LD = 0 : REM LEVEL DURATION
8870 LET LX = 20 : REM LAST X SNAKE
8880 LET LY = 39 : REM LAST Y SNAKE
8890 LET KE = 0 : REM KEYBOARD
8900 LET DX = 0 : LET DY = 0 : REM SNAKE DIRECTION
8910 CALL 40672 : REM UNHOOK DOS
9000 REM HOME SCREEN
9010 TEXT : HOME
9020 VTAB 10 : FLASH : A$ =  "SNAKE APPLE EATER" : GOSUB 7000 : NORMAL
9030 A$ = "PRESS ANY KEY" : GOSUB 7000 
9040 I = 0
9050 I = I + 1
9060 IF I > 1000 THEN I = 0
9070 KE = PEEK(49152)
9080 IF KE < 128 GOTO 9050
9090 POKE -16368,0
9100 REM LEVEL MANAGEMENT
9110 TEXT : HOME
9120 RESTORE : REM READ DATE FROM BEGINNING
9130 IF LV = 5 GOTO 9500
9140 FOR  J = 1 TO LV : READ NA, LD : NEXT J
9150 REM TEXT : HOME
9160 VTAB 14: INVERSE : A$ = "LEVEL "+STR$(LV) : GOSUB 7000 : NORMAL
9170 A$ = "PRESS KEY TO START" : GOSUB 7000
9180 GET A$
9190 GOTO 2000
9499 END
9500 REM END OF GAME
9510 TEXT : HOME
9520 VTAB 12 : A$= "END OF GAME" : GOSUB 7000
9525 IF NA = 0 THEN A$ = "VICTORY - YOU ATE THEM ALL !" : GOSUB 7000
9530 A$ = "FINAL SCORE : " + STR$(SC) : GOSUB 7000
9535 CALL 43089 : REM HOOK DOS
9540 END
10000 DATA 2,40,3,40,3,35,4,35
